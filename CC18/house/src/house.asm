;  **************************
; * orion-128.2 "HOUSE" 256B *
; *         VERSION 3        *
; *   BY RIDDLE (C) 2017,8   *
;  **************************
ORG 0
START:
LXI H,0F800H
LXI B,0F0A9H
JMP CLS
;-RST 1 -> HL - ADDRESS, C - COLOR, A - PIXELS
SETCLR:
MOV M,A
MVI A,1
EI
JMP 0F839H
;-
RAND:EQU START+1
;-
;-RST 2
RDLY:
RST 6
RST 7
JC RDLY
RPE
JMP RDLY
;-RST 3 -> HL - ADDRESS, DE - PIXELS, B - COUNT, C - COLOR
BXP16:
RST 5
DCR B
JNZ BXP16
RET
NOP
;-RST 4
WIN14:
PUSH H
PUSH H
LXI B,42BFH
JMP WIN140
;-RST 5 -> HL - ADDRESS, DE - PIXELS, C - COLOR
P16:
MOV A,D
RST 1
INR H
MOV A,E
RST 1
DCR H
DCR L
RET
;-RST 6
DLY:
PUSH B
PUSH H
CALL 0F82DH
POP H
POP B
RET
;-RST 7
RND:
PUSH H
LHLD RAND
MOV A,L
INR L
ADC M
SHLD RAND
POP H
RET
;-
WINS:
LXI H,0CED1H
CALL WIN
MVI H,0DAH
WIN:
LXI D,0E000H
RST 4
INX D
INX D ; DE=1
RST 4
LXI D,8000H
RST 4
LXI D,7
RST 4
RET
WIN140:
RST 3
RST 6 ; DE=0
DCX D ; DE=-1
MVI B,3
RST 3
POP H
MVI B,3
RST 3
POP H
INR H
INR H
RET
;-
;-
;-
CLS:
MVI M,6 ; 16-COLOR MODE
MVI H,0C0H
CLR:
MOV A,L
CMP C
SBB L ; SKY OR GRASS
RST 1
INX H
MOV A,H
CMP B
JNZ CLR
MAIN:
LXI H,0C8F0H
LXI D,1FE7H
M1:
PUSH H
M2:
PUSH D
PUSH H
;-
BRICK:
LXI B,484H
RST 6 ; DE=0
RST 5
LXI D,7FFEH
RST 3
;-
INR B ; B=1
MOV C,L ; NEW L
POP H
POP D
INR H
INR H
MOV A,H
CMP E
JC M2
POP PSW
XRA B
MOV H,A
MOV L,C ; NEW L
ANA L
JM M1
DCR D
JZ FINISH
DCR D
DCR E
MOV A,H
XRA B
MOV H,A
INR H
JMP M1
;-
FINISH:
CALL WINS
;-
PUSH B
CAT:
POP B
RST 2
RST 2
MVI L,0CFH ; !!!
RST 7
ANI 0CH
MOV H,A
ANI 8
RRC
ADD H
ADD L ; !!!
MOV H,A
LXI B,1B0H
CAT1:
MVI A,13 ; BITMAP ROWS
CMP B
PUSH B
JNC CAT2 ; IF B<=A: Y=B
RLC
SUB B
JZ CAT
MOV B,A  ; ELSE: Y=2*A-B
CAT2:
MOV A,L
SUB B
MOV L,A
LXI D,CATBM-1
;- HL - ADDRESS, DE - SPRITE, B - COUNT
S16:
LDAX D
INX D
INR H
RST 1
LDAX D
INX D
DCR H
RST 1
INR L
DCR B
JNZ S16
;-
DI ; PURR SOUND
RST 6
POP B
INR B
JMP CAT1
;-
CATBM:
DB 0
DW 0C003H,0A005H,9009H,8BD1H
DW 0A425H,0B00DH,8001H,8E71H
DW 4C32H,4182H,45A2H,2814H
;-
END
